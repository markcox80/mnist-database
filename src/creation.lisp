(in-package "MNIST-DATABASE")

(defun write-32-bit-integer (stream integer)
  (declare (type (unsigned-byte 32) integer))
  (com.gigamonkeys.binary-data:write-value 'com.gigamonkeys.binary-data.common-datatypes:u4 stream integer))

(defun write-unsigned-byte (stream integer)
  (declare (type (unsigned-byte 8) integer))
  (com.gigamonkeys.binary-data:write-value 'com.gigamonkeys.binary-data.common-datatypes:u1 stream integer))

(defun create-image-data-file (pathname dimensions sequence &key (key #'identity) (if-exists :error))
  (declare (type list dimensions))
  (assert (= 2 (length dimensions)))
  (assert (every #'integerp dimensions))
  (let ((number-of-pixels (* (first dimensions) (second dimensions))))
    (with-open-file (stream pathname :direction :output :if-exists if-exists :element-type '(unsigned-byte 8))
      (write-32-bit-integer stream 2051)
      (write-32-bit-integer stream (length sequence))
      (write-32-bit-integer stream (first dimensions))
      (write-32-bit-integer stream (second dimensions))

      (map nil #'(lambda (item)
		   (let ((image (funcall key item)))
		     (assert (equal dimensions (array-dimensions image)))
		     (assert (equal '(unsigned-byte 8) (array-element-type image)))
		     (write-sequence (make-array number-of-pixels :element-type '(unsigned-byte 8) :displaced-to image)
				     stream)))
	   sequence)))
  pathname)

(defun create-label-data-file (pathname sequence &key (key #'identity) (if-exists :error))
  (with-open-file (stream pathname :direction :output :if-exists if-exists :element-type '(unsigned-byte 8))
    (write-32-bit-integer stream 2049)
    (write-32-bit-integer stream (length sequence))

    (map nil #'(lambda (item)
		 (let ((label (funcall key item)))
		   (assert (valid-label-p label))
		   (write-unsigned-byte stream label)))
	 sequence))
  nil)
